# Snakemake workflow for epigenetic clock analysis
# This workflow processes TCGA projects independently to avoid reprocessing all clocks

import os
import csv
import glob
from snakemake.io import directory

include: "rules/get_project.smk"

# Get all TCGA projects from the queries file
PROJECTS = get_projects()

# Default clock for analysis
CLOCK = "Horvath"

os.makedirs("logs/clock/gdc_pan", exist_ok=True)
os.makedirs("logs/gdc", exist_ok=True)

# Final output
rule all:
    input:
        "results/clock/gdc_pan/gdc_pancan_methylclock.pdf",
        f"results/clinical/{CLOCK}_clinical_associations.csv",
        f"results/clinical/{CLOCK}_odds_ratio_plot.png",
        f"results/clinical/{CLOCK}_contingency_heatmaps.pdf",
        f"results/clinical/{CLOCK}_pvalue_heatmap.png",
        "data/processed/gdc_pancan/rnaseq_available_samples.tsv",
        f"results/tissue_analysis/{CLOCK}_full_quartile_distribution.csv",
        f"results/tissue_analysis/{CLOCK}_project_distribution.png"


# Download RNA-seq data for a specific project
rule download_rnaseq_project:
    input:
        query = "data/processed/gdc_pancan/rnaseq/{project}_query.rds"
    output:
        data_dir = directory("data/raw/GDCrnaseq/{project}")
    params:
        project = "{project}"
    log:
        "logs/gdc/download_rnaseq_{project}.log"
    script:
        "../scripts/clock/download_rnaseq_project.R"

rule only_process_project:
    input:
        expand("results/clock/gdc_pan/{project}_predictions.rds", project=PROJECTS)

# Download phenotype, build queries, and fetch GDC data
rule download_gdc:
    output:
        pheno = "data/processed/gdc_pancan/methyl/normal_pheno.tsv",
        counts = "data/processed/gdc_pancan/methyl/normal_counts_by_project.csv",
        queries = "data/processed/gdc_pancan/methyl/queries.rds",
        missing = "data/processed/gdc_pancan/methyl/missing_ids.rds",
        # gdcdata = directory("data/raw/GDCdata") intentionally omitted to avoid re-downloading
    log:
        "logs/gdc/download.log"
    script:
        "../scripts/clock/download_gdc_pancan.R"

# Process individual project and generate predictions + plots
rule process_project:
    input:
        queries = "data/processed/gdc_pancan/methyl/queries.rds",
        data = "data/raw/GDCdata/"
    output:
        scatter = "results/clock/gdc_pan/{project}_scatterplots.png",
        residuals = "results/clock/gdc_pan/{project}_residuals_boxplots.png",
        predictions = "results/clock/gdc_pan/{project}_predictions.rds"
    wildcard_constraints:
        project = "^(?!gdc_pancan$).+"
    params:
        project = "{project}"
    log:
        "logs/gdc/clock/{project}.log"
    script:
        "../scripts/clock/process_project.R"


# Check RNA-seq availability for each project independently
rule check_rnaseq_project:
    input:
        pheno = "data/processed/gdc_pancan/methyl/normal_pheno.tsv"
    output:
        available = "data/processed/gdc_pancan/rnaseq/{project}_available.tsv",
        query = "data/processed/gdc_pancan/rnaseq/{project}_query.rds"
    params:
        project = "{project}"
    log:
        "logs/gdc/rnaseq/{project}.log"
    script:
        "../scripts/clock/check_rnaseq_project.R"

rule only_check_rnaseq:
    input:
        expand("data/processed/gdc_pancan/rnaseq/{project}_available.tsv", project=PROJECTS)


# Combine all project RNA-seq availability lists
rule combine_rnaseq_availability:
    input:
        project_lists = expand("data/processed/gdc_pancan/rnaseq/{project}_available.tsv", project=PROJECTS)
    output:
        combined = "data/processed/gdc_pancan/rnaseq_available_samples.tsv"
    log:
        "logs/gdc/combine_rnaseq_availability.log"
    script:
        "../scripts/clock/combine_rnaseq_availability.R"

# Combine all project predictions and generate pancan-wide analysis
# Optionally filtered to samples with RNA-seq if rnaseq_available_samples.tsv exists
rule combine_pancan:
    input:
        queries = "data/processed/gdc_pancan/methyl/queries.rds",
        predictions = expand("results/clock/gdc_pan/{project}_predictions.rds", project=PROJECTS)
    output:
        scatter = "results/clock/gdc_pan/gdc_pancan_scatterplots.png",
        residuals = "results/clock/gdc_pan/gdc_pancan_residuals_boxplots.png",
        combined = "results/clock/gdc_pan/gdc_pancan_predictions.rds"
    log:
        "logs/clock/gdc_pan/pancan.log"
    script:
        "../scripts/clock/combine_pancan.R"

# Generate final PDF combining all plots
rule generate_pdf:
    input:
        scatter_plots = expand("results/clock/gdc_pan/{project}_scatterplots.png", project=PROJECTS),
        residual_plots = expand("results/clock/gdc_pan/{project}_residuals_boxplots.png", project=PROJECTS),
        pancan_scatter = "results/clock/gdc_pan/gdc_pancan_scatterplots.png",
        pancan_residuals = "results/clock/gdc_pan/gdc_pancan_residuals_boxplots.png"
    output:
        "results/clock/gdc_pan/gdc_pancan_methylclock.pdf"
    log:
        "logs/clock/gdc_pan/generate_pdf.log"
    script:
        "../scripts/clock/generate_pdf.R"

# Run clinical correlation analysis for a specified clock (default Horvath)
rule clinical_analysis:
    input:
        predictions = "results/clock/gdc_pan/gdc_pancan_predictions.rds",
        pheno = "data/processed/gdc_pancan/methyl/normal_pheno.tsv"
    output:
        assoc = f"results/clinical/{CLOCK}_clinical_associations.csv",
        odds = f"results/clinical/{CLOCK}_odds_ratio_plot.png",
        contingency = f"results/clinical/{CLOCK}_contingency_heatmaps.pdf",
        pheat = f"results/clinical/{CLOCK}_pvalue_heatmap.png"
    params:
        clock = CLOCK
    log:
        "logs/clock/gdc_pan/clinical_analysis.log"
    script:
        "../scripts/clinical_correlation.R"

# Analyze tissue type distribution across aging quartiles
rule tissue_distribution:
    input:
        predictions = "results/clock/gdc_pan/gdc_pancan_predictions.rds",
        pheno = "data/processed/gdc_pancan/normal_pheno.tsv"
    output:
        full_quartile = f"results/tissue_analysis/{CLOCK}_full_quartile_distribution.csv",
        project_dist = f"results/tissue_analysis/{CLOCK}_project_distribution.csv",
        full_plot = f"results/tissue_analysis/{CLOCK}_full_quartile_distribution.png",
        project_plot = f"results/tissue_analysis/{CLOCK}_project_distribution.png",
        project_pct = f"results/tissue_analysis/{CLOCK}_project_percentage.png"
    params:
        clock = CLOCK
    log:
        "logs/clock/gdc_pan/tissue_distribution.log"
    shell:
        "Rscript scripts/tissue_distribution.R {params.clock} &> {log}"


